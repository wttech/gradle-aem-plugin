plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version "1.2.21"
    id "com.jfrog.bintray" version "1.7.3"
}

group 'com.cognifide.gradle'
version '3.0.8-SNAPSHOT'
description = 'Gradle AEM Plugin'
defaultTasks = ['clean', 'build', 'publishToMavenLocal']

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile "org.jetbrains.kotlin:kotlin-stdlib:1.2.21"
    compile 'org.jetbrains.kotlin:kotlin-reflect:1.2.21'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-io:commons-io:2.4'
    compile 'commons-validator:commons-validator:1.6'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.8.8'
    compile 'org.reflections:reflections:0.9.9'
    compile 'org.apache.jackrabbit.vault:vault-cli:3.1.24'
    compile 'org.jsoup:jsoup:1.10.3'
    compile 'org.samba.jcifs:jcifs:1.3.18-kohsuke-1'
    compile 'biz.aQute.bnd:biz.aQute.bnd.gradle:3.5.0'
    compile group: 'org.zeroturnaround', name: 'zt-zip', version: '1.11'
    compile group: 'com.hierynomus', name: 'sshj', version: '0.21.1'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.4'
    compile group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.4'
    compile group: 'org.osgi', name: 'org.osgi.core', version: '6.0.0'
    compile group: 'com.mitchellbosecke', name: 'pebble', version: '2.4.0'

    testCompile gradleTestKit()
    testCompile 'junit:junit:4.12'
}

gradlePlugin {
    plugins {
        base {
            id = "com.cognifide.aem.base"
            implementationClass = "com.cognifide.gradle.aem.base.BasePlugin"
        }
        pkg {
            id = "com.cognifide.aem.package"
            implementationClass = "com.cognifide.gradle.aem.pkg.PackagePlugin"
        }
        bundle {
            id = "com.cognifide.aem.bundle"
            implementationClass = "com.cognifide.gradle.aem.bundle.BundlePlugin"
        }
        instance {
            id = "com.cognifide.aem.instance"
            implementationClass = "com.cognifide.gradle.aem.instance.InstancePlugin"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

test.dependsOn publishToMavenLocal
check.dependsOn publishToMavenLocal

bintray {
    user = project.properties.get('bintrayUser') ?: System.getenv('BINTRAY_USER')
    key = project.properties.get('bintrayKey') ?: System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven-public'
        name = 'gradle-aem-plugin'
        userOrg = 'cognifide'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Cognifide/gradle-aem-plugin.git'
        labels = ['aem', 'cq', 'vault', 'scr']
        version {
            name = project.version
            desc = "${project.description} ${project.version}"
            vcsTag = project.version
        }
    }
    publish = true
}