plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version "1.1.1"
    id "com.jfrog.bintray" version "1.7.3"
}

group 'com.cognifide.gradle'
version '1.2.0'
description = 'Gradle AEM Plugin'
defaultTasks = ['clean', 'publishToMavenLocal']

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile "org.jetbrains.kotlin:kotlin-stdlib:1.1.1"
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-io:commons-io:2.4'
    compile 'commons-httpclient:commons-httpclient:3.1'
    compile group: 'commons-validator', name: 'commons-validator', version: '1.6'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.8.8'
    compile 'org.reflections:reflections:0.9.11'
    compile 'org.dm.gradle:gradle-bundle-plugin:0.10.0'
    compile 'org.apache.jackrabbit.vault:org.apache.jackrabbit.vault:3.1.26'
    compile 'org.apache.jackrabbit.vault:vault-cli:3.1.6'
    compile 'org.jsoup:jsoup:1.10.3'
}

gradlePlugin {
    plugins {
        aem {
            id = "cognifide.aem"
            implementationClass = "com.cognifide.gradle.aem.AemPlugin"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

bintray {
    user = project.properties.get('bintrayUser') ?: System.getenv('BINTRAY_USER')
    key = project.properties.get('bintrayKey') ?: System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven-public'
        name = 'gradle-aem-plugin'
        userOrg = 'cognifide'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Cognifide/gradle-aem-plugin.git'
        labels = ['aem', 'cq', 'vault', 'scr']
        version {
            name = project.version
            desc = "${project.description} ${project.version}"
            vcsTag = project.version
        }
    }
    publish = true
}